import groovy.json.JsonSlurper

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "30.0.5"
        gradlePluginVersion = '4.1.2'
        minSdkVersion = 21
        compileSdkVersion = 30
        targetSdkVersion = 30
        // Some dependencies still expect supportLibVersion to be defined
        supportLibVersion = "29.0.0"
        kotlinVersion = '1.4.21'
    }
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:${gradlePluginVersion}")

        // Copied version from the Exponent Android project.
        // Newer versions suffer either from "play-services-basement was supposed to be 15.0.1,
        // but has been resolved to 17.0.0"
        // or https://github.com/segment-integrations/analytics-android-integration-firebase/issues/23
        classpath 'com.google.gms:google-services:4.3.5'  // Google Services plugin

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

def packageJsonFile = new File(rootProject.projectDir, '../package.json')
def packageJson = new JsonSlurper().parseText(packageJsonFile.text)

// [Custom]
ext {
    reactNativeVersion = packageJson.dependencies["react-native"]
}

allprojects {
    // [Custom]
    configurations.all {
        resolutionStrategy.force "com.facebook.react:react-native:${reactNativeVersion}"
    }

    repositories {
        mavenLocal()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url "$rootDir/../node_modules/react-native/android"
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }
        maven {
            // expo-camera bundles a custom com.google.android:cameraview
            url "$rootDir/../node_modules/expo-camera/android/maven"
        }

       maven {
           // Detox is installed from workspace root npm
           url "$rootDir/../../../node_modules/detox/Detox-android"
       }

        google()
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}
